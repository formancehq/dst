build-manifest output workload_tag='daily':
	rm -f -- '{{output}}'
	cat manifests/namespace.yaml >> '{{output}}'
	echo "---" >> '{{output}}'
	cat manifests/postgres.yaml >> '{{output}}'
	echo "---" >> '{{output}}'
	helm template regions oci://ghcr.io/formancehq/helm/regions --version 2.15.2 --namespace formance-systems >> '{{output}}'
	echo "---" >> '{{output}}'
	cat manifests/stack.yaml >> '{{output}}'
	echo "---" >> '{{output}}'
	yq '.spec.version = strenv(LEDGER_PREVIOUS_TAG)' manifests/ledger.yaml >> '{{output}}'
	echo "---" >> '{{output}}'
	yq '.spec.containers[0].image = "us-central1-docker.pkg.dev/molten-verve-216720/formance-repository/workload:{{workload_tag}}"' manifests/workload.yaml >> '{{output}}'

push faults='true':
	#!/usr/bin/env bash

	set -euxo pipefail
	tmpdir=$(mktemp -d)
	trap 'rm -rf $tmpdir' EXIT

	[ "{{faults}}" = 'true' ] && tag=daily_faults || tag=daily_nofaults

	just build-manifest "$tmpdir/resources.yaml" "$tag"

	docker build -f Dockerfile.config -t "$ANTITHESIS_REPOSITORY/antithesis-config:$tag" $tmpdir
	docker push "$ANTITHESIS_REPOSITORY/antithesis-config:$tag"
